rule "Morning On Rule"
when
	Time cron "0 55 06 ? * MON,TUE,WED,THU,FRI *" or
	Item Morning received command ON
then
	logWarn("Bigboxer23", "Morning On " + VacationMode.state + " " + now.getMonthOfYear)
	IsNight.sendCommand(OFF)
	IsLateNight.sendCommand(OFF)
	if (VacationMode.state != ON)
	{
		MorningOn.sendCommand(100)
		MorningOn.sendCommand(ON)
		UnderCabinets.sendCommand(35)
		StairsAccent.sendCommand(35)
		if (now.getMonthOfYear < 3 || now.getMonthOfYear > 10)
		{
			WinterMorningOn.sendCommand(100)
			LivingRoomBack.sendCommand(50)
		}
		if (now.getMonthOfYear == 12)
        {
            ChristmasLights.sendCommand(ON)
        }
	} else
	{
		logWarn("Bigboxer23", "Vacation Mode Enabled, not triggering")
	}
end

rule "Morning Fans Off Rule"
when
	Time cron "0 45 07 ? * MON,TUE,WED,THU,FRI *"
then
	logWarn("Bigboxer23", "Fans off")
	DeaneFan.sendCommand(OFF)
	NatalieFan.sendCommand(OFF)
	MasterBedroomFan.sendCommand(OFF)
end

rule "Morning Off"
when
	Time cron "0 0 9 ? * SUN,MON,TUE,WED,THU,FRI,SAT *" or
	Item Morning received command OFF
then
	logWarn("Bigboxer23", "Morning Off")
	EveningOffOff.sendCommand(0)
	EveningOffOff.sendCommand(OFF)
	MorningOff.sendCommand(0)
	MorningOff.sendCommand(OFF)
	SmartLivingRoom.sendCommand(0)
	//CoolingSetpoint.sendCommand(73|Â°F)
	if (now.getMonthOfYear == 12)
	{
		ChristmasLights.sendCommand(OFF)
	}
end