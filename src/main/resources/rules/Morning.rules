rule "Morning On Rule"
when
	Item IsMorning received command ON or
	Item Morning received command ON
then
	logWarn("Bigboxer23", "Morning On " + VacationMode.state + " " + now.getMonthOfYear)
	if (VacationMode.state != ON && IsPTO.state != ON && IsWeekend.state != ON)
	{
		MorningOn.sendCommand(100)
		MorningOn.sendCommand(ON)
		UnderCabinets.sendCommand(35)
		StairsAccent.sendCommand(35)
		if (now.getMonthOfYear < 3 || now.getMonthOfYear > 10)
		{
			WinterMorningOn.sendCommand(100)
			LivingRoomBack.sendCommand(100)
		}
	} else
	{
		logWarn("Bigboxer23", "Vacation, PTO or Weekend Mode Enabled, not triggering")
	}
	if (VacationMode.state != ON && IsPTO.state != ON)//Trigger even on weekend
	{
		SunRoomCeilingFan.sendCommand(ON)
		DiningRoomCurtains.sendCommand(ON)
		if (now.getMonthOfYear == 12)
        {
            ChristmasLights.sendCommand(ON)
        }
	}
end

rule "Morning Fans Off Rule"
when
	Time cron "0 45 07 ? * MON,TUE,WED,THU,FRI *"
then
	logWarn("Bigboxer23", "Fans off")
	if (IsPTO.state != ON)
	{
		DeaneFan.sendCommand(OFF)
		NatalieFan.sendCommand(OFF)
		MasterBedroomFan.sendCommand(OFF)
	}
end

rule "Morning Off"
when
	Item IsDay received command ON or
	Item Morning received command OFF
then
	logWarn("Bigboxer23", "Morning Off")
	EveningOffOff.sendCommand(0)
	EveningOffOff.sendCommand(OFF)
	MorningOff.sendCommand(0)
	MorningOff.sendCommand(OFF)
	SmartLivingRoom.sendCommand(0)
	//CoolingSetpoint.sendCommand(73|Â°F)
	if (now.getMonthOfYear == 12)
	{
		ChristmasLights.sendCommand(OFF)
	}
end