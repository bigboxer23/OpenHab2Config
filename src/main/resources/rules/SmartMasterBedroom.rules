rule "Smart Master Bedroom Rule"
when
	Item SmartMasterBedroom received command
then
	logWarn("Bigboxer23", "Smart Master Bedroom: " + receivedCommand + " " + now.getHourOfDay + " " + MattsBedside.state + " " + MasterBedroomFan.state)
	if (receivedCommand == OFF || receivedCommand == 0)
	{
        MasterBedroomOverhead.sendCommand(receivedCommand)
        Hallway.sendCommand(receivedCommand)
        MasterCloset.sendCommand(receivedCommand)
        MainAccent.sendCommand(receivedCommand)
        MattsOverheadAccent.sendCommand(receivedCommand)
        MaureensOverheadAccent.sendCommand(receivedCommand)
        MasterBedroomCornerLamp.sendCommand(receivedCommand)
		if (now.getHourOfDay >= 7 && now.getHourOfDay < 21)
        {
            MasterBedroomFan.sendCommand(OFF)
            MattsBedside.sendCommand(receivedCommand)
            MaureensBedside.sendCommand(receivedCommand)
        } else
        {
            var aCommand = 0;
            if (MasterBedroomFan.state == OFF)
            {
                aCommand = 25
            }
            MasterBedroomFan.sendCommand(ON)
            if (IsNight.state == OFF)
            {
                IsEvening.sendCommand(OFF)
                IsNight.sendCommand(ON)
            }
            SmartMasterBathroom.sendCommand(OFF)
            MattsBedside.sendCommand(aCommand)
            MaureensBedside.sendCommand(aCommand)
        }
	} else
	{
		var aCommand = receivedCommand;
		if (aCommand == ON)
		{
			aCommand = 100;
			if (now.getHourOfDay < 7 || now.getHourOfDay >= 21)
	        {
	            aCommand = 25;
	        }
		}
		if (now.getHourOfDay < 7 || now.getHourOfDay >= 20)
		{
			MasterBedroomFan.sendCommand(ON)
			MasterCloset.sendCommand(0)
            MasterBedroomOverhead.sendCommand(0)
            MainAccent.sendCommand(0)
            Hallway.sendCommand(0)
            MattsOverheadAccent.sendCommand(0)
            MaureensOverheadAccent.sendCommand(0)
            MasterBedroomCornerLamp.sendCommand(0)
		}
		if (now.getHourOfDay > 7 && now.getHourOfDay < 20)
        {
			MasterBedroomOverhead.sendCommand(aCommand as Number)
        } else
        {
            MattsBedside.sendCommand(aCommand as Number)
            MaureensBedside.sendCommand(aCommand as Number)
        }
	}
end

rule "Shades Closed For Moon"
when
    Time cron "0 00 21 ? * * *"
then
    val nowTime  = now
    val nextFull = new DateTime(FullMoonTime.state.toString)
    val prevFull = nextFull.minusDays(29) // approx previous full moon

    val withinWindow = [ DateTime target |
        val before = target.minusDays(3)
        val after  = target.plusDays(3)
        nowTime.isAfter(before) && nowTime.isBefore(after)
    ]

    val target = if (withinWindow.apply(prevFull)) prevFull else nextFull
    val within = withinWindow.apply(target)

    logWarn("Bigboxer23",
        "Shades Closed For Moon within=" + within +
        " target=" + target +
        " next=" + nextFull +
        " prev~=" + prevFull)

    if (within) {
        executeCommandLine(
            "curl -k https://bigboxer23.loclx.io/S/switchbot/setPosition/B0E9FEE11A08/100?t=" + kInternalToken.label,
            5000
        )
    }
end


rule "Shades Open For Moon"
when
    Time cron "0 00 09 ? * SUN,SAT *" or
     Time cron "0 10 07 ? * MON,TUE,WED,THU,FRI *"
then
    val nowTime  = now
    val nextFull = new DateTime(FullMoonTime.state.toString)

    // Approx previous full moon (good enough for a +/- few day window)
    val prevFull = nextFull.minusDays(29)

    val withinWindow = [ DateTime target |
        val before = target.minusDays(3)
        val after  = target.plusDays(4)
        nowTime.isAfter(before) && nowTime.isBefore(after)
    ]

    val target = if (withinWindow.apply(prevFull)) prevFull else nextFull
    val within = withinWindow.apply(target)

    logWarn("Bigboxer23",
        "Shades Open For Moon within=" + within +
        " target=" + target +
        " next=" + nextFull +
        " prev=" + prevFull)

    if (within) {
        executeCommandLine(
            "curl -k https://bigboxer23.loclx.io/S/switchbot/setPosition/B0E9FEE11A08/0?t=" + kInternalToken.label,
            5000
        )
    }
end
